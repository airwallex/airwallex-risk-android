apply plugin: "maven-publish"
apply plugin: "signing"

static def decodeBase64Key(encodedString) {
    if (encodedString != null) {
        byte[] decoded = encodedString.decodeBase64()
        String decode = new String(decoded)
        return decode
    } else {
        return null
    }
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId airwallexGroupId
                artifactId airwallexArtifactId
                version rootProject.airwallexSdkVersion
                artifact bundleReleaseAar

                pom {
                    name = airwallexName
                    description = airwallexDescription
                    url = airwallexUrl
                    licenses {
                        license {
                        }
                    }
                    organization {
                        name = airwallexOrganizationName
                        url = airwallexOrganizationUrl
                    }
                    developers {
                        developer {
                            organization = airwallexOrganizationName
                            organizationUrl = airwallexOrganizationUrl
                        }
                    }
                    scm {
                        connection = airwallexScmConnection
                        developerConnection = airwallexScmConnection
                        url = airwallexScmUrl
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                decodeBase64Key(System.getenv('SIGNING_KEY_BASE64')),
                System.getenv('SIGNING_PASSWORD')
        )
        sign publishing.publications
    }
}